package eu.trentorise.game.model
import eu.trentorise.game.core.Utility;

global Utility utils;

rule 'ch_absoluteIncrement_check'
	salience -2000
when
	$challenge : ChallengeConcept( modelName == "absoluteIncrement", $counter: fields["counterName"], $target: fields["target"], $periodName: fields["periodName"] , $bonusPointType : fields["bonusPointType"], $bonusScore : fields["bonusScore"], isCompleted() != true )
	$pc1 : PointConcept(name == (String) $counter, $score : score, getPeriodCurrentScore((String)$periodName) >= $target)
	$pc : PointConcept(name == (String) $bonusPointType)
then
	utils.log('absoluteIncrement COMPLETED');
	$challenge.completed();
	double newScore = $pc.getScore() + (Double) $bonusScore; 
	modify($pc) { setScore(newScore) }
	update($challenge);
end
